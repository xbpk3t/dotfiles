{
  mylib,
  config,
  lib,
  myvars,
  pkgs,
  ...
}:
with lib; let
  cfgWayland = config.modules.desktop.wayland;
in {
  imports = mylib.scanPaths ./.;

  options.modules.desktop = {
    wayland = {
      enable = mkEnableOption "Wayland Display Server";
    };
  };

  config = mkMerge [
    (mkIf cfgWayland.enable {
      ####################################################################
      #  NixOS's Configuration for Wayland based Window Manager
      ####################################################################
      services = {
        xserver = {
          enable = false; # disable xorg server
        };
        # Explicitly disable GDM (using new syntax)
        displayManager.gdm.enable = false;
        # https://wiki.archlinux.org/title/Greetd
        greetd = {
          enable = true;
          settings = {
            default_session = {
              # Wayland Desktop Manager is installed only for user ryan via home-manager!
              user = myvars.username;
              # .wayland-session is a script generated by home-manager, which links to the current wayland compositor(sway/hyprland or others).
              # with such a vendor-no-locking script, we can switch to another wayland compositor without modifying greetd's config here.
              command = "$HOME/.wayland-session"; # start a wayland session directly without a login manager
              # command = "${pkgs.greetd.tuigreet}/bin/tuigreet --time --cmd $HOME/.wayland-session";  # start wayland session with a TUI login manager
            };
          };
        };

        displayManager = {
          enable = false; # disable any display manager
          defaultSession = "none";
          sessionPackages = [];
        };
      };

      # fix https://github.com/ryan4yin/nix-config/issues/10
      security.pam.services.swaylock = {};

      # Enable graphical target
      systemd.defaultUnit = "graphical.target";
    })
    {
      environment.systemPackages = with pkgs; [
        deadnix
        statix
        alejandra
        namaka
        nixtract
        nix-unit
        hydra-check
        nix-init
        nix-melt
        nix-tree
      ];
    }
  ];
}
